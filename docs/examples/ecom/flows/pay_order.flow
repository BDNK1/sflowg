// Create payment for an order via stripe-integration service

entrypoint.http {
    method: POST
    path: /api/orders/:id/pay
    pathVariables: [id]
}

step fetch_order {
    postgres.get({
        query: "SELECT id, customer_email, amount_cents, currency, status FROM orders WHERE id = $1",
        params: [request.pathVariables.id]
    })
}

step not_found(condition: fetch_order.found == false) {
    response.json({
        status: 404,
        body: { error: "order not found" }
    })
}

step already_paid(condition: fetch_order.row.status == "paid") {
    response.json({
        status: 409,
        body: { error: "order is already paid" }
    })
}

step already_canceled(condition: fetch_order.row.status == "canceled") {
    response.json({
        status: 409,
        body: { error: "canceled order cannot be paid" }
    })
}

step payment_pending(condition: fetch_order.row.status == "payment_pending") {
    response.json({
        status: 409,
        body: { error: "payment is already in progress for this order" }
    })
}

step create_payment {
    http.request({
        url: properties.stripe_integration_url + "/api/payments",
        method: "POST",
        content_type: "json",
        body: {
            amount: fetch_order.row.amount_cents,
            currency: fetch_order.row.currency,
            description: `Order #${fetch_order.row.id}`,
            customer_email: fetch_order.row.customer_email,
            metadata: {
                order_id: `${fetch_order.row.id}`
            }
        }
    })
}

step payment_error(condition: create_payment.status_code != 201 || create_payment.body.success != true) {
    let message = create_payment.body.error?.message ?? "payment service error"
    response.json({
        status: 502,
        body: {
            error: "failed to create payment via stripe-integration",
            details: message
        }
    })
}

step save_payment_intent {
    postgres.get({
        query: "UPDATE orders SET status = 'payment_pending', stripe_payment_intent_id = $1, stripe_client_secret = $2 WHERE id = $3 RETURNING id, customer_email, amount_cents, currency, status, stripe_payment_intent_id, stripe_client_secret, created_at, paid_at, canceled_at",
        params: [create_payment.body.data.payment_intent_id, create_payment.body.data.client_secret, request.pathVariables.id]
    })
}

return response.json({
    status: 200,
    body: {
        order: save_payment_intent.row,
        stripe: {
            checkout_url: create_payment.body.data.checkout_url,
            payment_intent_id: create_payment.body.data.payment_intent_id,
            status: create_payment.body.data.status
        }
    }
})
